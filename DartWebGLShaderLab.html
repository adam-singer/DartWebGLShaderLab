<html>
  <head>
    <title>WebGL Shader Lab</title>
    
    <script id="shader-vs" type="x-shader/x-vertex"> 
	  attribute vec3 aPos;
	  attribute vec2 aTexCoord;
	  varying   vec2 pixel;
	void main(void) {
	   gl_Position = vec4(aPos, 1.);
	   pixel = aTexCoord;
	}
	</script>
	
	<script id="shader-fs-blur-horizontal" type="x-shader/x-fragment">
	#ifdef GL_ES
	precision highp float;
	#endif
	// original shader from http://www.gamerendering.com/2008/10/11/gaussian-blur-filter-shader/
	// horizontal blur fragment shader
	uniform sampler2D src_tex;
	varying vec2 pixel;
	uniform vec2 pixelSize;
	void main(void) // fragment
	{
		float h = pixelSize.x;
		vec4 sum = vec4(0.0);
		sum += texture2D(src_tex, vec2(pixel.x - 4.0*h, pixel.y) ) * 0.05;
		sum += texture2D(src_tex, vec2(pixel.x - 3.0*h, pixel.y) ) * 0.09;
		sum += texture2D(src_tex, vec2(pixel.x - 2.0*h, pixel.y) ) * 0.12;
		sum += texture2D(src_tex, vec2(pixel.x - 1.0*h, pixel.y) ) * 0.15;
		sum += texture2D(src_tex, vec2(pixel.x + 0.0*h, pixel.y) ) * 0.16;
		sum += texture2D(src_tex, vec2(pixel.x + 1.0*h, pixel.y) ) * 0.15;
		sum += texture2D(src_tex, vec2(pixel.x + 2.0*h, pixel.y) ) * 0.12;
		sum += texture2D(src_tex, vec2(pixel.x + 3.0*h, pixel.y) ) * 0.09;
		sum += texture2D(src_tex, vec2(pixel.x + 4.0*h, pixel.y) ) * 0.05;
	    gl_FragColor.xyz = sum.xyz/0.98; // normalize
		gl_FragColor.a = 1.;
	} 
	</script>
	
	<script id="shader-fs-blur-vertical" type="x-shader/x-fragment">
	#ifdef GL_ES
	precision highp float;
	#endif
	// original shader from http://www.gamerendering.com/2008/10/11/gaussian-blur-filter-shader/
	// vertical blur fragment shader
	uniform sampler2D src_tex;
	varying vec2 pixel;
	uniform vec2 pixelSize;
	void main(void) // fragment
	{
		float v = pixelSize.y;
		vec4 sum = vec4(0.0);
		sum += texture2D(src_tex, vec2(pixel.x, - 4.0*v + pixel.y) ) * 0.05;
		sum += texture2D(src_tex, vec2(pixel.x, - 3.0*v + pixel.y) ) * 0.09;
		sum += texture2D(src_tex, vec2(pixel.x, - 2.0*v + pixel.y) ) * 0.12;
		sum += texture2D(src_tex, vec2(pixel.x, - 1.0*v + pixel.y) ) * 0.15;
		sum += texture2D(src_tex, vec2(pixel.x, + 0.0*v + pixel.y) ) * 0.16;
		sum += texture2D(src_tex, vec2(pixel.x, + 1.0*v + pixel.y) ) * 0.15;
		sum += texture2D(src_tex, vec2(pixel.x, + 2.0*v + pixel.y) ) * 0.12;
		sum += texture2D(src_tex, vec2(pixel.x, + 3.0*v + pixel.y) ) * 0.09;
		sum += texture2D(src_tex, vec2(pixel.x, + 4.0*v + pixel.y) ) * 0.05;
	    gl_FragColor.xyz = sum.xyz/0.98;
		gl_FragColor.a = 1.;
	}
	</script>
	
	<script id="shader-fs-advance" type="x-shader/x-fragment"> 
	#ifdef GL_ES
	precision highp float;
	#endif
	
		uniform sampler2D sampler_prev;
		uniform sampler2D sampler_prev_n;
		uniform sampler2D sampler_blur;
		uniform sampler2D sampler_noise;
		uniform sampler2D sampler_noise_n;
	
		varying vec2 pixel;
		uniform vec2 pixelSize;
		uniform vec4 rnd;
		uniform vec2 mouse;
		uniform float time;
		uniform float fps;
	
	void main(void) {
		// grabbing the blurred gradients
		vec2 d = pixelSize*4.;
		vec4 dx = (texture2D(sampler_blur, pixel + vec2(1,0)*d) - texture2D(sampler_blur, pixel - vec2(1,0)*d))*0.5;
		vec4 dy = (texture2D(sampler_blur, pixel + vec2(0,1)*d) - texture2D(sampler_blur, pixel - vec2(0,1)*d))*0.5;
		
		vec2 zoom_in = pixel + vec2(dx.x,dy.x)*pixelSize*8.; // adding the traveling wave front
		vec2 rand_noise = texture2D(sampler_noise, zoom_in + vec2(rnd.x, rnd.y)).xy;
		gl_FragColor.x = texture2D(sampler_prev, zoom_in).x + (rand_noise.x-0.5)*0.0025 - 0.002; // decay with error diffusion
		gl_FragColor.x -= (texture2D(sampler_blur, zoom_in + (rand_noise-0.5)*pixelSize).x -
						  texture2D(sampler_prev, zoom_in + (rand_noise-0.5)*pixelSize)).x*0.047; // reaction-diffusion
	
		gl_FragColor.a = 1.;
	}
	</script>
	
	<script id="shader-fs-composite" type="x-shader/x-fragment"> 
	#ifdef GL_ES
	precision highp float;
	#endif
	
		uniform sampler2D sampler_prev;
		uniform sampler2D sampler_prev_n;
		uniform sampler2D sampler_blur;
		uniform sampler2D sampler_noise;
		uniform sampler2D sampler_noise_n;
	
		varying vec2 pixel;
		uniform vec2 pixelSize;
		uniform vec2 aspect;
		uniform vec4 rnd;
		uniform vec2 mouse;
		uniform float time;
	
	void main(void) {
	
		vec2 lightSize=vec2(4.);
	
		// grabbing the blurred gradients
		vec2 d = pixelSize*2.;
		vec4 dx = (texture2D(sampler_blur, pixel + vec2(1,0)*d) - texture2D(sampler_blur, pixel - vec2(1,0)*d))*0.5;
		vec4 dy = (texture2D(sampler_blur, pixel + vec2(0,1)*d) - texture2D(sampler_blur, pixel - vec2(0,1)*d))*0.5;
	
		// adding the pixel gradients
		d = pixelSize*1.;
		dx += texture2D(sampler_prev, pixel + vec2(1,0)*d) - texture2D(sampler_prev, pixel - vec2(1,0)*d);
		dy += texture2D(sampler_prev, pixel + vec2(0,1)*d) - texture2D(sampler_prev, pixel - vec2(0,1)*d);
	
		vec2 displacement = vec2(dx.x,dy.x)*lightSize; // using only the red gradient as displacement vector
		float light = pow(max(1.-distance(0.5+(pixel-0.5)*aspect*lightSize + displacement,0.5+(mouse-0.5)*aspect*lightSize),0.),4.);
	//	vec4 rd = 0.85-(1.-texture2D(sampler_prev,pixel-vec2(dx.x,dy.x)*pixelSize*2.5).x) + light*0.18;
		//rd = mix(rd, 1., light*(1.-texture2D(sampler_blur,pixel-vec2(dx.x,dy.x)*pixelSize*4.)).x); 
	
		// recoloring the lit up red channel
		vec4 rd = vec4(texture2D(sampler_prev,pixel+vec2(dx.x,dy.x)*pixelSize*8.).x)*vec4(0.7,1.5,2.0,1.0)-vec4(0.3,1.0,1.0,1.0);
		gl_FragColor = mix(rd,vec4(8.0,6.,2.,1.), light*0.75*vec4(1.-texture2D(sampler_prev,pixel+vec2(dx.x,dy.x)*pixelSize*8.).x)); 
		
		//gl_FragColor = texture2D(sampler_prev, pixel); // bypass
		gl_FragColor.a = 1.;
	}
	</script>
    
    
    <style type="text/css">
	body {
		background-color: #000000;
		color: #FFFFFF;
		text-shadow: #000000;
	}
	
	#c {
		position: absolute;
		top: 0;
		left: 0;
		z-index: -1;
	}
	</style>
  </head>
  <body>
    <h1 id="fps"></h1>

	<canvas id="c"></canvas>
    <script type="application/dart" src="DartWebGLShaderLab.dart"></script>
    <script src="http://dart.googlecode.com/svn/branches/bleeding_edge/dart/client/dart.js"></script>
  </body>
</html>
